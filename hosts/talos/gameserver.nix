{ config, lib, pkgs, ... }:
let
  serverPort = 25565;
  rconPort = 25575;

  jvmOpts = "-Xms4G -Xmx6G";
  # jvmOpts = "-Xms4G -Xmx4G -XX:+UseG1GC -XX:+CMSIncrementalPacing -XX:+CMSClassUnloadingEnabled -XX:ParallelGCThreads=2 -XX:MinHeapFreeRatio=5 -XX:MaxHeapFreeRatio=10";

  eulaFile = builtins.toFile "eula.txt" ''
    # eula.txt managed by NixOS Configuration
    eula=true
  '';
  serverProperties = {
    motd = "The UPS Store";
    server-port = serverPort;
    difficulty = 3;
    gamemode = 0;
    max-players = 24;
    enable-rcon = true;
    "rcon.port" = rconPort;
    "rcon.password" = "stupidpassword";
  };

  cfgToString = v: if builtins.isBool v then boolToString v else toString v;

  serverPropertiesFile = pkgs.writeText "server.properties" (''
    # server.properties managed by NixOS configuration
  '' + concatStringsSep "\n" (mapAttrsToList
    (n: v: "${n}=${cfgToString v}") serverProperties));

  stopScript = pkgs.writeShellScript "minecraft-server-stop" ''
    echo stop > ${config.systemd.sockets.minecraft-ups.socketConfig.ListenFIFO}

    # Wait for the PID of the minecraft server to disappear before
    # returning, so systemd doesn't attempt to SIGKILL it.
    while kill -0 "$1" 2> /dev/null; do
      sleep 1s
    done
  '';
in {
  users = {
    groups.minecraft = {};
    extraUsers.minecraft = {
      isSystemUser = true;
      group = "minecraft";
      home = "/mnt/ssd/gameservers/minecraft-ups";
      createHome = true;
      packages = with pkgs; [
        temurin-jre-bin-17
      ];
    };
  };

  systemd.sockets.minecraft-ups = {
    bindsTo = [ "minecraft-ups.service" ];
    socketConfig = {
      ListenFIFO = "/run/minecraft-ups.stdin";
      SocketMode = "0660";
      SocketUser = "minecraft";
      SocketGroup = "minecraft";
      RemoveOnStop = true;
      FlushPending = true;
    };
  };

  systemd.services.minecraft-ups = {
    enable = true;
    description = "Forge Minecraft Server";
    # after = [ "network.target" ];
    # wantedBy = [ "default.target" ];
    wantedBy      = [ "multi-user.target" ];
    requires      = [ "minecraft-ups.socket" ];
    after         = [ "network.target" "minecraft-ups.socket" ];
    serviceConfig = {
      WorkingDirectory = "${config.users.extraUsers.minecraft.home}";
      ExecStart = "${pkgs.temurin-jre-bin-17}/bin/java ${jvmOpts} @libraries/net/minecraftforge/forge/1.20.1-47.2.19/unix_args.txt";
      # ExecStart = "${pkgs.temurin-jre-bin-17}/bin/java ${jvmOpts}";
      ExecStop = "${stopScript} $MAINPID";
      Restart = "always";
      RestartSec = 60;
      User = "minecraft";

      StandardInput = "socket";
      StandardOutput = "journal";
      StandardError = "journal";

      # Hardening
      CapabilityBoundingSet = [ "" ];
      DeviceAllow = [ "" ];
      LockPersonality = true;
      PrivateDevices = true;
      PrivateTmp = true;
      PrivateUsers = true;
      ProtectClock = true;
      ProtectControlGroups = true;
      ProtectHome = true;
      ProtectHostname = true;
      ProtectKernelLogs = true;
      ProtectKernelModules = true;
      ProtectKernelTunables = true;
      ProtectProc = "invisible";
      RestrictAddressFamilies = [ "AF_INET" "AF_INET6" ];
      RestrictNamespaces = true;
      RestrictRealtime = true;
      RestrictSUIDSGID = true;
      SystemCallArchitectures = "native";
      UMask = "0077";
    };
    prestart = ''
      ln -sf ${eulaFile} eula.txt
      if [ -e .declarative ]; then

        # Was declarative before, no need to back up anything
        cp -f ${serverPropertiesFile} server.properties

      else

        # Declarative for the first time, backup stateful files
        cp -b --suffix=.stateful ${serverPropertiesFile} server.properties

        # server.properties must have write permissions, because every time
        # the server starts it first parses the file and then regenerates it..
        chmod +w server.properties
        echo "Autogenerated file that signifies that this server configuration is managed declaratively by NixOS" \
          > .declarative

      fi
    '';
  };

  networking.firewall = {
    allowedUDPPorts = [ serverPort ];
    allowedTCPPorts = [ serverPort ]
      ++ optional (rconPort != null) rconPort;
  };

  # services = {
  #   minecraft-server = {
  #     enable = true;
  #     eula = true;
  #     openFirewall = true;
  #     declarative = true;
  #     dataDir = "/mnt/ssd/gameservers/minecraft-ups";
  #     serverProperties = {
  #       motd = "The UPS Store";
  #       server-port = 25565;
  #       difficulty = 3;
  #       gamemode = 0;
  #       max-players = 24;
  #       enable-rcon = true;
  #       "rcon.port" = 25575;
  #       "rcon.password" = "stupidpassword";
  #     };
  #   };
  # };
}
